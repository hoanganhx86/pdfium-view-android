apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets{
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
}

version = '1.0.0'
group = 'com.github.mshockwave'

gradle.taskGraph.whenReady { taskGraph ->
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    allprojects { ext."signing.keyId" = properties.get("signing.ketId") }
    allprojects { ext."signing.password" = properties.get("signing.password") }
    allprojects { ext."signing.secretKeyRingFile" = properties.get("signing.secretKeyRingFile") }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    //required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Android Pdfium Library'
            packaging 'aar'
            description 'Android Pdfium Application Library for API Level greater than 9'
            url 'https://github.com/mshockwave/PdfiumAndroid'

            scm {
                url 'scm:git@github.com:mshockwave/PdfiumAndroid.git'
                connection 'scm:git@github.com:mshockwave/PdfiumAndroid.git'
                developerConnection 'scm:git@github.com:mshockwave/PdfiumAndroid.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'mshockwave'
                    name 'Bekket McClane'
                    email 'yihshyng223@gmail.com'
                }
            }
        }
    }
}
